buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'gwt-base'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.goharsha"
archivesBaseName = "easyxml"
version = "0.1"

// Check if the required properties are included, so we prevent our
// build from failing.
if (project.hasProperty('ossrhUsername'))
{
    project.ext.ossrhUsername = ossrhUsername
    project.ext.ossrhPassword = ossrhPassword
}
else
{
    // We might be running on another system where he is not
    // the owner who can push to OSSRH through maven. Set
    // default values so he can still build the library locally.
    project.ext.ossrhUsername = ''
    project.ext.ossrhPassword = ''
}

repositories {
    jcenter()
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task gwtJar(type: Jar) {
    classifier = "gwt"
    from sourceSets.main.allSource
    from sourceSets.main.output
}

artifacts {
    archives gwtJar, javadocJar, sourcesJar
}

dependencies {
    testCompile 'junit:junit:4.12'
}

signing {
    // Only sign the archives if there is a username. This allows one to build the library
    // on systems with no username set.
    if (ossrhUsername != '')
        sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.ext.ossrhUsername, password: project.ext.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.ext.ossrhUsername, password: project.ext.ossrhPassword)
            }

            pom.project {
                name 'EasyXML'
                packaging 'jar'
                description 'An easy to use GWT compatible XML parser for Java'
                url 'https://github.com/sriharshachilakapati/EasyXML/'

                scm {
                    connection 'scm:git:git@github.com:sriharshachilakapati/EasyXML.git'
                    developerConnection 'scm:git:git@github.com:sriharshachilakapati/EasyXML.git'
                    url 'git@github.com:sriharshachilakapati/EasyXML.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'sriharshachilakapati'
                        name 'Sri Harsha Chilakapati'
                        email 'sriharshachilakapati@gmail.com'
                    }
                }
            }
        }
    }
}
